#!/usr/bin/env zsh
# vim:ft=zsh ts=2 sw=2 sts=2

autoload colors; colors;
setopt prompt_subst

parse_git_dirty() {
  local GIT_STATUS=''
  GIT_STATUS=$(command git status -s 2> /dev/null | tail -n1)
  if [[ -n $GIT_STATUS ]]; then
    echo 1
  fi
}

parse_git_branch() {
  ref=$(git symbolic-ref HEAD 2> /dev/null) || ref="➦ $(git show-ref --head -s --abbrev 2> /dev/null | head -n1 2> /dev/null)"
  echo ${ref#refs/heads/}
}

parse_git_changes() {
  if $(git remote 2> /dev/null | grep -iq 'origin') ;then
    local git_where="$(parse_git_branch)"
    behind=$(git rev-list HEAD..origin/${git_where} --count 2> /dev/null)
    ahead=$(git rev-list origin/${git_where}..HEAD --count 2> /dev/null)
    echo " ↑$ahead ↓$behind"
  else
    echo " ?"
  fi
}

CURRENT_BG='NONE'
CURRENT_FG='NONE'

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
prompt_segment() {
  local b f
  [[ -n $1 ]] && b="$bg[$1]" || b="$bg[black]"
  [[ -n $2 ]] && f="$fg[$2]" || f="$fg[white]"

  if [[ $CURRENT_BG != 'NONE' ]]; then
    echo -n " "
  fi

  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n "%{$b$fg[$CURRENT_BG]$f%}"
  else
    echo -n "%{$b$f%}"
  fi
  CURRENT_BG=$1
  CURRENT_FG=$2
  [[ -n $3 ]] && echo -n $3
}

# Context: user@hostname (who am I and where am I)
prompt_context() {
	local user=`whoami`
  prompt_segment default default "%{$fg_bold[blue]%}$user%{$reset_color%}%{$fg[white]%}@%m"
}

# End the prompt, closing any open segments
prompt_end() {
  echo -n "%{$reset_color%}"
  CURRENT_BG=''
}

# Git: branch/detached head, dirty status
prompt_git() {
  local ref dirty mode repo_path
  repo_path=$(git rev-parse --git-dir 2>/dev/null)

  git rev-parse --is-inside-work-tree >/dev/null 2>&1

  if [ $? -eq 0 ]; then
    dirty=$(parse_git_dirty)
    if [[ -n $dirty ]]; then
      prompt_segment default red
    else
      prompt_segment default green
    fi

    if [[ -e "${repo_path}/BISECT_LOG" ]]; then
      mode=" <B>"
    elif [[ -e "${repo_path}/MERGE_HEAD" ]]; then
      mode=" >M<"
    elif [[ -e "${repo_path}/rebase" || -e "${repo_path}/rebase-apply" || -e "${repo_path}/rebase-merge" || -e "${repo_path}/../.dotest" ]]; then
      mode=" >R>"
    fi

    setopt promptsubst
    echo -n "%{$fg_bold[$CURRENT_FG]%}$(parse_git_branch)%{$reset_color%}%{$fg[$CURRENT_FG]%}$(parse_git_changes)${mode}"
	fi
}

# Dir: current working directory
prompt_dir() {
  prompt_segment default blue '%~'
}

prompt_time() {
  prompt_segment default white "`date +%H:%M:%S`"
}

## Main prompt
build_prompt() {
  RETVAL=$?
  prompt_time
  prompt_context
  prompt_dir
  prompt_git
  prompt_end
}

PROMPT=$'%{$reset_color%}$(build_prompt)%{$reset_color%}\n%{$fg[yellow]%}\U2605%{$reset_color%}  '
